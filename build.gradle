plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'com.google.cloud.tools.jib' version '3.2.1'
}

group = 'online.partyrun'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

jar {
    enabled = false
}

jib {
    from {
        image = "openjdk:17"
    }
    to {
        tags = ["latest"]
        image = "parkseongwoo/party-run-authentication"
    }
    container {
        jvmFlags = ["-Xms128m", "-Xmx128m"]
    }

    extraDirectories {
        paths = file('build/resources/main/static/docs')
    }
}


repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    configurations 'asciidoctorExt'
    baseDirFollowsSourceFile()
    inputs.dir snippetsDir

    dependsOn test
}

asciidoctor.doFirst {
    delete file('build/resources/main/static/docs')
}

task createDocument(type: Copy) {
    dependsOn asciidoctor

    from file("${asciidoctor.outputDir}")
    into file("build/resources/main/static/docs")
}

bootJar {
    dependsOn createDocument
}

dependencies {
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.google.firebase:firebase-admin:9.1.1'
    implementation 'com.github.SWM-KAWAI-MANS:jwt-manager:1.2.0'

    //lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.1.Final'
    testImplementation 'com.github.SWM-KAWAI-MANS:test-manager:1.0.0'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.1.Final'
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'


    //lombok-test
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.1.Final'
    testAnnotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
}
